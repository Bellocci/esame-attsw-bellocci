apply plugin: "org.sonarqube"
apply plugin: 'jacoco'

jacocoTestReport {
	dependsOn build
	
	//  Allow to insert integrationTest and e2eTest also
	executionData { tasks.withType(Test).findAll { it.jacoco.destinationFile.exists() }*.jacoco.destinationFile }
	
    reports {
    	xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    
    // Remove Book, Library and LibrarySwingView classes from JaCoCo report
	afterEvaluate {
    	getClassDirectories().setFrom(
    		files(classDirectories.files.collect {
        		fileTree(dir: it, exclude: [
        			"**/Library.class",
        			"**/Book.class",
        			"**/HibernateUtil.class",
        			"**/LibrarySwingApp.class"
        		])
        	})
        )
    }
}

sonarqube {
  properties {
    property "sonar.projectKey", "Bellocci_esame-attsw-bellocci"
    property "sonar.organization", "belloccifrancesco-github"
    property "sonar.host.url", "https://sonarcloud.io"
    
    property "sonar.coverage.exclusions", "**/LibrarySwingApp.java, **/Book.java, " + 
    	"**/Library.java, **/HibernateUtil.java"
    
    property "sonar.projectName", "Esame attsw Bellocci"	
	property "sonar.sourceEncoding", "UTF-8"
	property "sonar.sources", "src/main/java"	
	property "sonar.junit.reportPaths", 
		[
			"$buildDir/test-results/test", 
			"$buildDir/test-results/integrationTest",
			"$buildDir/test-results/e2eTest"
		]
		
	// Add integrationTest srcDirs into sonar.test property
    Collection<File> integrationTestSourceDirs = project.sourceSets.integrationTest.allJava.srcDirs.findAll { File dir -> dir.exists() }
    properties['sonar.tests'] += integrationTestSourceDirs
    // Add integrationTest output files into property sonar.java.test.binaries
    Collection<File> integrationTestsClasses = project.sourceSets.integrationTest.output.classesDirs.files.findAll { File file -> file.exists() }
    properties['sonar.java.test.binaries'] += integrationTestsClasses
    
    // Add e2eTest srcDirs into sonar.test property
    Collection<File> e2eTestSourceDirs = project.sourceSets.e2eTest.allJava.srcDirs.findAll { File dir -> dir.exists() }
    properties['sonar.tests'] += e2eTestSourceDirs
    // Add e2eTest output files into property sonar.java.test.binaries
    Collection<File> e2eTestsClasses = project.sourceSets.e2eTest.output.classesDirs.files.findAll { File file -> file.exists() }
    properties['sonar.java.test.binaries'] += e2eTestsClasses
	
	property 'sonar.coverage.jacoco.xmlReportPaths', "$project.projectDir/build/reports/jacoco/test/jacocoTestReport.xml"
    
    /*
     * Ignore rules
     */

    property "sonar.issue.ignore.multicriteria", "e11, e12, e13, e14, e15"
    
    // Removed rule because some test cannot have assertion
    property "sonar.issue.ignore.multicriteria.e11.ruleKey", "java:S2699"
    property "sonar.issue.ignore.multicriteria.e11.resourceKey", "**/BookSwingViewTest.java"
    
    property "sonar.issue.ignore.multicriteria.e12.ruleKey", "java:S2699"
    property "sonar.issue.ignore.multicriteria.e12.resourceKey", "**/LibrarySwingViewTest.java"
    
    property "sonar.issue.ignore.multicriteria.e13.ruleKey", "java:S2699"
    property "sonar.issue.ignore.multicriteria.e13.resourceKey", "**/BookSwingViewIT.java"
    
    // Removed rule because class name follow convention for e2e test
    
    property "sonar.issue.ignore.multicriteria.e14.ruleKey", "java:S3577"
    property "sonar.issue.ignore.multicriteria.e14.resourceKey", "**/LibrarySwingAppE2E.java"
    
    // Rule for ignoring variable name of *SwingView.class
    
    property "sonar.issue.ignore.multicriteria.e15.ruleKey","java:S117"
    property "sonar.issue.ignore.multicriteria.e15.resourceKey", "**/*SwingView.java"
  }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"